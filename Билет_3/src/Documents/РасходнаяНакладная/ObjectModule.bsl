
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаПоДокументу = СписокНоменклатуры.Итог("Сумма");
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Если НЕ ЭтоНовый() Тогда
		Движения.ОстаткиНоменклатуры.Записать();
	КонецЕсли;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = СписокНоменклатуры;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	МИНИМУМ(РасходнаяНакладнаяСписокНоменклатуры.НомерСтроки) КАК НомерСтроки,
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество
		|ПОМЕСТИТЬ ВТ_Товары
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОстаткиНоменклатурыОстатки.Номенклатура КАК Номенклатура,
		|	ОстаткиНоменклатурыОстатки.Партия,
		|	ОстаткиНоменклатурыОстатки.КоличествоОстаток,
		|	ОстаткиНоменклатурыОстатки.СуммаОстаток
		|ПОМЕСТИТЬ ВТ_Остатки
		|ИЗ
		|	РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|			&МоментВремени,
		|			Номенклатура В
		|				(ВЫБРАТЬ
		|					ВТ_Товары.Номенклатура
		|				ИЗ
		|					ВТ_Товары КАК ВТ_Товары)) КАК ОстаткиНоменклатурыОстатки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Товары.НомерСтроки КАК НомерСтроки,
		|	ВТ_Товары.Номенклатура КАК Номенклатура,
		|	ВТ_Товары.Количество КАК Количество,
		|	ВТ_Остатки.Партия,
		|	ЕСТЬNULL(ВТ_Остатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(ВТ_Остатки.СуммаОстаток, 0) КАК СуммаОстаток,
		|	ВТ_Товары.Номенклатура.Представление КАК Представление,
		|	ВТ_Товары.Номенклатура.Услуга КАК Услуга
		|ИЗ
		|	ВТ_Товары КАК ВТ_Товары
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Остатки КАК ВТ_Остатки
		|		ПО ВТ_Товары.Номенклатура = ВТ_Остатки.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВТ_Остатки.Партия.МоментВремени
		|ИТОГИ
		|	МАКСИМУМ(НомерСтроки),
		|	МАКСИМУМ(Количество),
		|	СУММА(КоличествоОстаток),
		|	МАКСИМУМ(Услуга)
		|ПО
		|	Номенклатура";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
		
		Если ВыборкаНоменклатура.Услуга Тогда 
			Продолжить;
		КонецЕсли;
		
		Если ВыборкаНоменклатура.Количество>ВыборкаНоменклатура.КоличествоОстаток Тогда 
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Недостаточно номенклатуры. Остаток: " + ВыборкаНоменклатура.КоличествоОстаток;
			Сообщение.Сообщить();
			Отказ = Истина;
		КонецЕсли;
		
		Если НЕ Отказ Тогда 		
			ВыборкаДетальныеЗаписи = ВыборкаНоменклатура.Выбрать();
			Списать = ВыборкаНоменклатура.Количество;
		
			Пока ВыборкаДетальныеЗаписи.Следующий() И Списать>0 Цикл
				Движение = Движения.ОстаткиНоменклатуры.Добавить();
				Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
				Движение.Период = Дата;
				Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
				Движение.Количество = Мин(Списать, ВыборкаДетальныеЗаписи.КоличествоОстаток);
				Движение.Сумма = ?(Движение.Количество = ВыборкаДетальныеЗаписи.КоличествоОстаток, ВыборкаДетальныеЗаписи.СуммаОстаток, (Движение.Количество/ВыборкаДетальныеЗаписи.КоличествоОстаток)*ВыборкаДетальныеЗаписи.СуммаОстаток);
			
				Списать = Списать - Движение.Количество;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	// регистр ОстаткиНоменклатуры Расход
	Движения.ОстаткиНоменклатуры.Записывать = НЕ Отказ;
	      	
	//{{__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!

	
	//}}__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
КонецПроцедуры
